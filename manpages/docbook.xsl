<?xml version='1.0'?>
<xsl:stylesheet xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
                xmlns:exsl="http://exslt.org/common"
                exclude-result-prefixes="exsl"
                version='1.0'>

<!-- ********************************************************************
     $Id$
     ********************************************************************

     This file is part of the XSL DocBook Stylesheet distribution.
     See ../README or http://docbook.sf.net/release/xsl/current/ for
     copyright and other information.

     ******************************************************************** -->

<!-- ==================================================================== -->

<xsl:import href="../html/docbook.xsl"/>
<xsl:include href="general.xsl"/>
<xsl:include href="block.xsl"/>
<xsl:include href="inline.xsl"/>
<xsl:include href="synop.xsl"/>
<xsl:include href="lists.xsl"/>
<xsl:include href="xref.xsl"/>

<!-- Needed for chunker.xsl (for now): -->
<xsl:param name="chunker.output.method" select="'text'"/>
<xsl:param name="chunker.output.encoding" select="'ISO-8859-1'"/>

<xsl:output method="text"
            encoding="ISO-8859-1"
            indent="no"/>

<!-- if document does not contain at least one refentry, then emit a -->
<!-- message and stop -->
<xsl:template match="/">
  <xsl:choose>
    <xsl:when test="//refentry">
      <xsl:apply-templates select="//refentry"/>
    </xsl:when>
    <xsl:otherwise>
      <xsl:message>No refentry elements!</xsl:message>
    </xsl:otherwise>
  </xsl:choose>
</xsl:template>

<!-- ============================================================== -->

<!--                The real fun starts here -->

<!-- ============================================================== -->

<xsl:template match="refentry">

  <xsl:variable name="get.metainfo">
    <xsl:call-template name="get.metainfo" />
  </xsl:variable>
  
  <xsl:variable name="metainfo" select="exsl:node-set($get.metainfo)"/>

  <xsl:call-template name="write.text.chunk">
    <xsl:with-param name="filename" select="$metainfo/filename"/>
    <xsl:with-param name="content">

      <!-- page header (commented-out stuff at top of roff source) -->
      <xsl:call-template name="page.header"/>
      
      <!-- .TH line (sets title stuff and begin and end)-->
      <xsl:call-template name="TH.title.section">
        <xsl:with-param name="reftitle" select="$metainfo/reftitle"/>
        <xsl:with-param name="section" select="$metainfo/section"/>
        <xsl:with-param name="date" select="$metainfo/date"/>
        <xsl:with-param name="productname" select="$metainfo/productname"/>
        <xsl:with-param name="title" select="$metainfo/title"/>
      </xsl:call-template>
      
      <!-- main body of man page -->
      <xsl:call-template name="main.body"/>

      <!-- AUTHOR section (at end of man page) -->
      <xsl:call-template name="author.section"/>
      
    </xsl:with-param>
  </xsl:call-template>
  <!-- Now generate stub include pages for every page documented in
       this refentry (except the page itself) -->
  <xsl:for-each select="refnamediv/refname">
    <xsl:if test=". != $metainfo/name">
      <xsl:call-template name="write.text.chunk">
        <xsl:with-param name="filename"
                        select="concat(normalize-space(.), '.',
                                $metainfo/section)"/>
        <xsl:with-param
            name="content"
            select="concat('.so man',
                    $metainfo/section,
                    '/',
                    $metainfo/name,
                    '.',
                    $metainfo/section,
                    '&#10;')"/>
      </xsl:call-template>
    </xsl:if>
  </xsl:for-each>
</xsl:template>

<xsl:template match="refmeta"></xsl:template>
<xsl:template match="title"></xsl:template>
<xsl:template match="abstract"></xsl:template>

<!-- ============================================================== -->

<!--        Named templates for each "part" of output page: -->

<!-- ============================================================== -->

<!-- * page header     comment part at top of roff source; unrendered -->
<!-- * TH line         "title" lines at top/botton of rendered page~ -->
<!-- * main body       the bulk of the rendered page -->
<!-- * AUTHOR section  last part of rendered page (usually)-->

<!-- ============================================================== -->

<xsl:template name="page.header">
      <xsl:text>.\"Generated by db2man.xsl. Don't modify this, modify the source.
.\"
. \"---------------------------------------------------
.de Sh \" - a second-level section (a DocBook refsect2)
.br
.if t .Sp
.ne 5
\fB\\$1\fR
.PP
..\"---------------------------------------------------
.de Sp \" - vertical space (when we can't use .PP)
.if t .sp .5v
.if n .sp
..\"---------------------------------------------------
.de Ip \" - a list item
.br
.ie \\n(.$>=3 .ne \\$3
.el .ne 3
.IP "\\$1" \\$2
..\"---------------------------------------------------
.\"</xsl:text>
<xsl:text>&#10;</xsl:text>
</xsl:template>

<xsl:template name="TH.title.section">
  <xsl:param name="title"/>
  <xsl:param name="reftitle"/>
  <xsl:param name="section"/>
  <xsl:param name="date"/>
  <xsl:param name="productname"/>

  <xsl:text>.\" "TITLE" "SECTION NUMBER" "DATE" "PRODUCT" "GROUP TITLE"</xsl:text>
  <xsl:text>&#10;</xsl:text>
      <xsl:text>.TH "</xsl:text>
      <xsl:value-of select="translate($reftitle,
                            'abcdefghijklmnopqrstuvwxyz',
                            'ABCDEFGHIJKLMNOPQRSTUVWXYZ')"/>
      <xsl:text>" </xsl:text>
      <xsl:value-of select="$section"/>
      <xsl:text> "</xsl:text>
      <xsl:value-of select="normalize-space($date)"/>
      <xsl:text>" "</xsl:text>
      <xsl:value-of select="normalize-space($productname)"/>
      <xsl:text>" "</xsl:text>
      <xsl:value-of select="$title"/>
      <xsl:text>"&#10;</xsl:text>
</xsl:template>

<xsl:template name="main.body">
  <xsl:apply-templates/>
</xsl:template>

<xsl:template name="author.section">
  <xsl:choose>
    <xsl:when test="info//author">
      <xsl:apply-templates select="info" mode="authorsect"/>
    </xsl:when>
    <xsl:when test="refentryinfo//author">
      <xsl:apply-templates select="refentryinfo" mode="authorsect"/>
    </xsl:when>
    <xsl:when test="/book/bookinfo//author">
      <xsl:apply-templates select="/book/bookinfo" mode="authorsect"/>
    </xsl:when>
    <xsl:when test="/article/articleinfo//author">
      <xsl:apply-templates select="/article/articleinfo" mode="authorsect"/>
    </xsl:when>
  </xsl:choose>
</xsl:template>

<!-- ============================================================== -->

<!--        Named template for getting metainfo from source  -->

<!-- ============================================================== -->

<xsl:template name="get.metainfo">

  <xsl:variable name="section">
    <xsl:choose>
      <xsl:when test="refmeta/manvolnum">
        <xsl:value-of select="refmeta/manvolnum[1]"/>
      </xsl:when>
      <xsl:when test=".//funcsynopsis">3</xsl:when>
      <xsl:otherwise>1</xsl:otherwise>
    </xsl:choose>
  </xsl:variable>

  <section>
    <xsl:value-of select="$section"/>
  </section>

  <xsl:variable name="name" select="refnamediv/refname[1]"/>

  <name>
    <xsl:value-of select="$name"/>
  </name>

  <!-- standard man page width is 64 chars; 6 chars needed for the two
       (x) volume numbers, and 2 spaces, leaves 56 -->
  <xsl:variable name="twidth" select="(56 - string-length(refmeta/refentrytitle)) div 2"/>

  <reftitle>
    <xsl:value-of select="substring(refmeta/refentrytitle, 1, $twidth)"/>
  </reftitle>

  <title>
    <xsl:choose>
      <xsl:when test="refentryinfo/title">
        <xsl:value-of select="refentryinfo/title"/>
      </xsl:when>
      <xsl:when test="../referenceinfo/title">
        <xsl:value-of select="../referenceinfo/title"/>
      </xsl:when>
    </xsl:choose>
  </title>

  <date>
    <xsl:choose>
      <xsl:when test="refentryinfo/date">
        <xsl:value-of select="refentryinfo/date"/>
      </xsl:when>
      <xsl:when test="../referenceinfo/date">
        <xsl:value-of select="../referenceinfo/date"/>
      </xsl:when>
    </xsl:choose>
  </date>

  <productname>
    <xsl:choose>
      <xsl:when test="refentryinfo/productname">
        <xsl:value-of select="refentryinfo/productname"/>
      </xsl:when>
      <xsl:when test="../referenceinfo/productname">
        <xsl:value-of select="../referenceinfo/productname"/>
      </xsl:when>
    </xsl:choose>
  </productname>

  <filename>
    <xsl:call-template name="replace-string">
      <!-- replace spaces in source filename with underscores in output filename -->
      <xsl:with-param name="content"
                      select="concat(normalize-space ($name), '.', $section)"/>
      <xsl:with-param name="replace" select="' '"/>
      <xsl:with-param name="with" select="'_'"/>
    </xsl:call-template>
  </filename>

</xsl:template>

<!-- ============================================================== -->

<xsl:template match="info|articleinfo|bookinfo|refentryinfo" mode="authorsect">
  <xsl:text>.SH "</xsl:text>
  <xsl:call-template name="string.upper">
    <xsl:with-param name="string">
      <xsl:call-template name="gentext">
        <xsl:with-param name="key" select="'Author'"/>
      </xsl:call-template>
    </xsl:with-param>
  </xsl:call-template>
  <xsl:text>"&#10;</xsl:text>

  <xsl:for-each select=".//author">
    <xsl:if test="position() > 1">
      <xsl:text>, </xsl:text>
    </xsl:if>
    <xsl:apply-templates select="."/>
  </xsl:for-each>
  <xsl:text>. &#10;</xsl:text>
  <xsl:if test=".//editor">
    <xsl:text>.br&#10;</xsl:text>
    <xsl:apply-templates select=".//editor"/>
    <xsl:text>. (man page)&#10;</xsl:text>
  </xsl:if>
  <xsl:for-each select="address">
  <xsl:text>.br&#10;</xsl:text>
    <xsl:apply-templates/>
    <xsl:text>&#10;</xsl:text>
  </xsl:for-each>
</xsl:template>

<xsl:template match="author|editor">
  <xsl:call-template name="person.name"/>
  <xsl:if test=".//email">
    <xsl:text> </xsl:text>
    <xsl:apply-templates select=".//email"/>
  </xsl:if>
</xsl:template>

<xsl:template match="copyright">
  <xsl:text>Copyright \(co  </xsl:text>
  <xsl:apply-templates select="./year" />
  <xsl:text>.Sp&#10;</xsl:text>
</xsl:template>

<xsl:template match="email">
  <xsl:text>&lt;</xsl:text>
  <xsl:apply-templates/>
  <xsl:text>&gt;</xsl:text>
</xsl:template>

</xsl:stylesheet>
